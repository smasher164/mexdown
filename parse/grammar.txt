unicode_char = /* an arbitrary Unicode code point except newline */ .
newline      = /* the Unicode code point U+000A */ .
tab          = /* the Unicode code point U+0009 */ .
octothorpe   = /* the Unicode code point U+0023 */ .
backtick     = /* the Unicode code point U+0060 */ .
hyphen       = /* the Unicode code point U+002D */ .
asterisk     = /* the Unicode code point U+002A */ .
lbrack       = /* the Unicode code point U+005B */ .
rbrack       = /* the Unicode code point U+005D */ .
lparen       = /* the Unicode code point U+0028 */ .
rparen       = /* the Unicode code point U+0029 */ .
underscore   = /* the Unicode code point U+005F */ .
colon        = /* the Unicode code point U+003A */ .

citation = lbrack text rbrack colon string .
paragraph = text .
list_item = { tab } hyphen [ lbrack text rbrack ] text .
list = { list_item newline } [ list_item ] .
string = { unicode_char | newline } .
command = unicode_char { unicode_char } .
dirbody = backtick dirbody backtick | [ command ] newline string .
directive = backtick backtick backtick dirbody backtick backtick backtick .
text = unicode_char { unicode_char } | 
       lbrack text rbrack lparen text rparen | 
       asterisk text asterisk | 
       asterisk asterisk text asterisk asterisk | 
       underscore text underscore | 
       hyphen hyphen text hyphen hyphen | 
       backtick text backtick .
header = octothorpe { octothorpe } text .
statement = header | directive | list | paragraph | citation .
source_file = { statement [ newline ] [ newline ] } .